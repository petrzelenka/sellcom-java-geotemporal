apply plugin: "java-library"

// ------------------------------------------------------------
// ------------------------------------------------------------
// ------------------------------------------------------------
// Configure build process

tasks.withType(JavaCompile) {
	sourceCompatibility = 1.10
	targetCompatibility = 1.10

	options.compilerArgs.add("-Xlint:all")
	options.compilerArgs.add("-Xpkginfo:always")
	options.encoding = "UTF-8"
}

javadoc {
	exclude "**/internal/**"

	options.addBooleanOption("html5", true)
	options.addStringOption("Xdoclint:all,-missing", "-quiet")
	options.encoding = "UTF-8"
	options.links = [ "https://docs.oracle.com/javase/10/docs/api/" ]
	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.splitIndex = true
	options.use = true
}

// ------------------------------------------------------------
// ------------------------------------------------------------
// ------------------------------------------------------------
// Configure artifacts

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"

	from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
	classifier "sources"

	from sourceSets.main.allSource
}

tasks.withType(Jar) {
	manifest {
		attributes("Created-By": System.getProperty("java.version") + " (" + System.getProperty("java.vendor") + ")")
		attributes("Implementation-Title": project.group + ":" + project.name)
		attributes("Implementation-Version": project.version)
	}
}

artifacts {
	archives javadocJar
	archives sourcesJar
}
